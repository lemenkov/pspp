dnl PSPP - a program for statistical analysis.
dnl Copyright (C) 2017 Free Software Foundation, Inc.
dnl
dnl This program is free software: you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation, either version 3 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program.  If not, see <http://www.gnu.org/licenses/>.
dnl
AT_BANNER([PSPP terminal UI])

AT_SETUP([nonexistent syntax file crash])
AT_CHECK([pspp nonexistent], [1],
  [error: Opening `nonexistent': No such file or directory.
])
AT_CLEANUP

AT_SETUP([SIGTERM yields clean shutdown])
AT_KEYWORDS([slow])
AT_DATA([main.sps], [dnl
INPUT PROGRAM.
COMPUTE x = x + 1.
DO IF x = 10000.
END CASE.
ELSE IF x < 0.
END FILE.
END IF.
END INPUT PROGRAM.
EXECUTE.
])
AT_CHECK([pspp main.sps & sleep 1; kill $!; wait $!], [143], [], [ignore])
AT_CLEANUP

AT_SETUP([SIGSEGV yields error report])
# This test intentionally causes SIGSEGV, so make Address Sanitizer ignore it.
ASAN_OPTIONS=$ASAN_OPTIONS:handle_segv=0; export ASAN_OPTIONS

AT_CHECK([[echo 'host command=["kill -SEGV $PPID"].' | pspp -O format=csv]],
  [139], [], [stderr])

cat > expout <<EOF
******************************************************
You have discovered a bug in PSPP.  Please report this
to $PACKAGE_BUGREPORT.  Please include this entire
message, *plus* several lines of output just above it.
For the best chance at having the bug fixed, also
include the syntax file that triggered it and a sample
of any data file used for input.
proximate cause:     Segmentation Violation
EOF

AT_CHECK([sed '/proximate/q' < stderr], [0], [expout])
AT_CLEANUP

dnl This tests for a crash which was observed with --syntax
AT_SETUP([argument parsing])

AT_DATA([main.sps], [dnl
ECHO 'This is a test'.
FINISH.
])

AT_CHECK([pspp --syntax=enhanced main.sps], [0], [ignore])
AT_CLEANUP

dnl Bug #63910 reported that command output was delayed until the
dnl next command was supplied.  This checks for regression against
dnl that bug.
AT_SETUP([interactive output appears immediately])
dnl ptys are pretty system-dependent and it's hard to test them
dnl everywhere.  For example, on Mac OS the SHOW N and FINISH command
dnl text doesn't appear in the output.  So we'll just skip them
dnl other than on the OS we know best.
AT_CHECK([case $host in #(
  *-linux*) ;; #(
  *) exit 77
esac])
dnl We have to use squish-pty to make PSPP think that we're running
dnl interactively.  First make sure that squish-pty works at all.
SQUISH_PTY="$PYTHON3 $top_srcdir/tests/ui/terminal/squish-pty.py"
AT_CHECK([$SQUISH_PTY true </dev/null >/dev/null 2>/dev/null || exit 77])
dnl Then do the real test.  The crucial thing to notice here is
dnl that the SHOW output must appear before the prompt for FINISH.
AT_CHECK([echo 'SHOW N.
FINISH.' | $SQUISH_PTY pspp], [0], [stdout])
AT_CHECK([sed -n 's/$//
/^PSPP>/,$p' stdout], [0], [dnl
PSPP> SHOW N.
  Settings
+-+-------+
|N|Unknown|
+-+-------+
PSPP> FINISH.
])
AT_CLEANUP
