nl PSPP - a program for statistical analysis.   -*- autotest -*-
dnl Copyright (C) 2017 Free Software Foundation, Inc.
dnl
dnl This program is free software: you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation, either version 3 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program.  If not, see <http://www.gnu.org/licenses/>.
dnl

AT_INIT

m4_ifdef([AT_COLOR_TESTS], [AT_COLOR_TESTS])

# Define SKIP_IF for compatibility with Autotest before 2.64
m4_ifndef([AT_SKIP_IF],
  [m4_define([AT_SKIP_IF],
     [AT_CHECK([($1) \
&& exit 77 || exit 0], [0], [ignore], [ignore])])])

m4_divert_text([PREPARE_TESTS], [dnl
if test X"$RUNNER" != X; then
    wrapper_dir=`pwd`/wrappers
    rm -f "$wrapper_dir"/*
    test -d "$wrapper_dir" || mkdir "$wrapper_dir"

    wrap_dir () {
        test -d "$1" || return
	for file in "$1"/*; do
            if test -x "$file" && test -f "$file"; then
		base=`basename $file $EXEEXT`
		wrapper=$wrapper_dir/$base
		if test ! -e "$wrapper"; then
		    echo "exec $RUNNER $file \"\$@\"" > $wrapper
		    chmod +x $wrapper
		fi
	    fi
	done
    }

    save_IFS=$IFS; IFS=$PATH_SEPARATOR
    for dir in $AUTOTEST_PATH; do
        IFS=$save_IFS
        test X"$dir" = X && dir=.
	case $dir in
            [\\/]* | ?:[\\/]*)
                wrap_dir "$dir"
		;;
            *)
	        wrap_dir "$abs_top_builddir/$dir"
		wrap_dir "$abs_top_srcdir/$dir"
		;;
	esac
    done
    IFS=$save_IFS
    PATH=$wrapper_dir:$PATH
fi
])

m4_divert_text([PREPARE_TESTS], [dnl
dnl ptys are pretty system-dependent and it's hard to test them
dnl everywhere.  For example, on Mac OS the SHOW N and FINISH command
dnl text doesn't appear in the output.  So we'll just skip them
dnl other than on the OS we know best.
AS_CASE([$host],
  [*-linux*],
  [dnl Make sure that squish-pty works.
   SQUISH_PTY="$PYTHON3 $abs_top_srcdir/tests/ui/terminal/squish-pty.py"
   if $SQUISH_PTY true </dev/null >/dev/null 2>/dev/null; then
       :
   else
       SQUISH_PTY=no
   fi],
  [SQUISH_PTY=no])
])
