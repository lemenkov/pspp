dnl PSPP - a program for statistical analysis.
dnl Copyright (C) 2017 Free Software Foundation, Inc.
dnl
dnl This program is free software: you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation, either version 3 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program.  If not, see <http://www.gnu.org/licenses/>.
dnl
AT_BANNER([DELETE VARIABLES])

dnl Checks for regressions against a crash reported in bug #38843.
AT_SETUP([DELETE VARIABLES with FILTER])
AT_DATA([delete-variables.sps], [dnl
DATA LIST LIST /a b.
BEGIN DATA.
1 3
4 6
7 9
END DATA.

FILTER BY b.
DELETE VARIABLES a.
LIST.
])
AT_CHECK([pspp -O format=csv delete-variables.sps], [0], [dnl
Table: Reading free-form data from INLINE.
Variable,Format
a,F8.0
b,F8.0

Table: Data List
b
3.00
6.00
9.00
])
AT_CLEANUP

dnl Checks for regression against a crash reported on pspp-users:
dnl https://lists.gnu.org/archive/html/pspp-users/2021-03/msg00025.html
AT_SETUP([DELETE VARIABLES with string variables])
AT_DATA([delete-variables.sps], [dnl
DATA LIST NOTABLE /s1 TO s2 1-2(A).
BEGIN DATA
12
END DATA.
DELETE VARIABLES s1.
NUMERIC n1.
LIST.
])
AT_CHECK([pspp -O format=csv delete-variables.sps], [0], [dnl
Table: Data List
s2,n1
2,.  @&t@
])
AT_CLEANUP

AT_SETUP([DELETE VARIABLES syntax errors])
AT_DATA([delete-variables.sps], [dnl
DATA LIST LIST NOTABLE /x y z.
BEGIN DATA.
1 2 3
END DATA.
TEMPORARY.
DELETE VARIABLES x.
DELETE VARIABLES y z.
])
AT_DATA([insert.sps], [dnl
INSERT FILE='delete-variables.sps' ERROR=IGNORE.
])
AT_CHECK([pspp --testing-mode -O format=csv insert.sps], [1], [dnl
"delete-variables.sps:6.1-6.16: error: DELETE VARIABLES: DELETE VARIABLES may not be used after TEMPORARY.  Temporary transformations will be made permanent.
    6 | DELETE VARIABLES x.
      | ^~~~~~~~~~~~~~~~"

"delete-variables.sps:7.1-7.20: error: DELETE VARIABLES: DELETE VARIABLES may not be used to delete all variables from the active dataset dictionary.  Use NEW FILE instead.
    7 | DELETE VARIABLES y z.
      | ^~~~~~~~~~~~~~~~~~~~"
])
AT_CLEANUP